apply plugin: 'com.android.application'

def tagCmd = 'git describe --tags'
def tag = tagCmd.execute().text.trim()
def date = new Date().format('yyyyMMdd').toInteger()

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "com.osfans.android2048"
        minSdkVersion 14
        targetSdkVersion 29
        versionCode date
        versionName "$tag-$date"
    }

    signingConfigs {
        release {
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-android.txt'
            signingConfig signingConfigs.release
        }
    }

    def propsFile = rootProject.file('gradle.properties')
    //store release config in gradle.properties
    def configName = 'release'

    if (propsFile.exists() && android.signingConfigs.hasProperty(configName)) {
        def props = new Properties()
        props.load(new FileInputStream(propsFile))
        if (props != null && props.containsKey('storeFile')) {
            android.signingConfigs[configName].storeFile = rootProject.file(props['storeFile'])
            android.signingConfigs[configName].storePassword = props['storePassword']
            android.signingConfigs[configName].keyAlias = props['keyAlias']
            android.signingConfigs[configName].keyPassword = props['keyPassword']
        }
    }
}

